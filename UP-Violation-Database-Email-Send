Sub SendEmailWithSubjectAndBody()

    Dim OutlookApp As Object
    Dim OutlookMail As Object
    Dim wb As Workbook
    Dim ws As Worksheet
    Dim emailSubject As String
    Dim emailBody As String
    Dim recipient As String
    Dim lastRow As Long
    Dim lastColumn As Long
    Dim i As Long, j As Long
    Dim filePath As String
    Dim cellValue As String

    ' Define the path to the Excel file (replace with a generic path)
    filePath = "C:\Path\To\Your\File.xlsx"

    ' Open the Excel file
    Set wb = Workbooks.Open(filePath)
    
    ' Set the worksheet to the specific tab (replace with a generic name)
    On Error Resume Next
    Set ws = wb.Sheets("Sheet1") 
    
    ' If the worksheet doesn't exist, display an error and exit the sub
    If ws Is Nothing Then
        MsgBox "Sheet not found in the workbook.", vbCritical
        wb.Close False
        Set wb = Nothing
        Exit Sub
    End If
    On Error GoTo 0

    ' Define email subject and recipient
    emailSubject = "Policy Compliance Notice"
    recipient = "someone@example.com"

    ' Find the last row and last column with data
    lastRow = ws.Cells(ws.Rows.Count, 1).End(xlUp).Row
    lastColumn = 9 ' Only considering columns A to I

    ' Apply filter on a specific column (replace with placeholder criteria)
    ws.Rows(1).AutoFilter Field:=11, Criteria1:="Placeholder"
    ws.Rows(1).AutoFilter Field:=15, Criteria1:="Company Name"

    ' Start the email body
    emailBody = "Dear Recipient," & vbCrLf & vbCrLf
    emailBody = "Please find below the requested data from the attached report." & vbCrLf & vbCrLf
    emailBody = emailBody & "<table border='1' cellpadding='5' cellspacing='0'>" & vbCrLf

    ' Add the table header row
    emailBody = emailBody & "<tr>"
    For j = 1 To lastColumn
        emailBody = emailBody & "<th>" & CStr(ws.Cells(1, j).Value) & "</th>"
    Next j
    emailBody = emailBody & "</tr>" & vbCrLf

    ' Loop through each row of data and add it to the table
    For i = 2 To lastRow
        If ws.Rows(i).Hidden = False Then
            emailBody = emailBody & "<tr>"
            For j = 1 To lastColumn
                cellValue = CStr(ws.Cells(i, j).Value)
                emailBody = emailBody & "<td>" & cellValue & "</td>"
            Next j
            emailBody = emailBody & "</tr>" & vbCrLf
        End If
    Next i

    ' Close the table tag
    emailBody = emailBody & "</table>" & vbCrLf
    emailBody = emailBody & "Best regards," & vbCrLf
    emailBody = emailBody & "Your Name"

    ' Create the Outlook application object
    Set OutlookApp = CreateObject("Outlook.Application")
    Set OutlookMail = OutlookApp.CreateItem(0)

    ' Set up the email details
    With OutlookMail
        .To = recipient
        .Subject = emailSubject
        .HTMLBody = emailBody
        .Send
    End With

    ' Close the workbook without saving changes
    wb.Close False

    ' Clean up
    Set OutlookMail = Nothing
    Set OutlookApp = Nothing
    Set ws = Nothing
    Set wb = Nothing

End Sub
